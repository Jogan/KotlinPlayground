ext {
  ci = System.getenv("CI") == "true"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
kapt {
  correctErrorTypes = true
}
apply plugin: 'kotlin-android-extensions'
androidExtensions {
  experimental = true
}

apply plugin: 'com.getkeepsafe.dexcount'
dexcount {
  // Only run the method counter on CI
  runOnEachPackage = ci
}

android {
  buildToolsVersion "28.0.0-rc2"
  compileSdkVersion 'android-P'

  defaultConfig {
    applicationId "com.jogan.kotlinplayground"
    minSdkVersion 21
    targetSdkVersion 'P'
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner 'com.jogan.kotlinplayground.test.TestRunner'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }

  dexOptions {
    // Don't pre-dex on CI
    preDexLibraries !ci
  }

  lintOptions {
    // Disable lintVital. Not needed since lint is run on CI
    checkReleaseBuilds false
  }

  // Always show the result of every unit test, even if it passes.
  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.41"

  // Android Jetpack
  implementation 'androidx.core:core-ktx:1.0.0-alpha1'
  implementation "androidx.appcompat:appcompat:1.0.0-alpha1"
  implementation "androidx.recyclerview:recyclerview:1.0.0-alpha1"
  implementation "androidx.constraintlayout:constraintlayout:1.1.0"
  implementation "com.google.android.material:material:1.0.0-alpha1"

  implementation "androidx.lifecycle:lifecycle-runtime:2.0.0-alpha1"
  implementation "androidx.lifecycle:lifecycle-extensions:2.0.0-alpha1"
  implementation "androidx.lifecycle:lifecycle-reactivestreams:2.0.0-alpha1"
  kapt "androidx.lifecycle:lifecycle-compiler:2.0.0-alpha1"

  implementation "androidx.paging:paging-runtime:2.0.0-alpha1"

  implementation "androidx.room:room-runtime:2.0.0-alpha1"
  implementation "androidx.room:room-rxjava2:2.0.0-alpha1"
  kapt "androidx.room:room-compiler:2.0.0-alpha1"

  // Other
  implementation "com.google.dagger:dagger-android:2.16"
  implementation "com.google.dagger:dagger-android-support:2.16"
  kapt "com.google.dagger:dagger-compiler:2.16"
  kapt "com.google.dagger:dagger-android-processor:2.16"

  implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"
  implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
  implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.0"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.1.0"

  implementation "javax.inject:javax.inject:1"

  implementation "com.squareup.moshi:moshi:1.5.0"
  implementation "com.squareup.okhttp3:okhttp:3.10.0"
  implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"
  implementation "com.squareup.retrofit2:retrofit:2.3.0"
  implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
  implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"

  implementation "com.github.bumptech.glide:glide:4.6.1"

  implementation "com.xwray:groupie:2.0.3"
  implementation "com.xwray:groupie-kotlin-android-extensions:2.0.3"

  implementation "com.jakewharton.timber:timber:4.6.1"

  debugImplementation "nl.littlerobots.rxlint:rxlint:1.6"

  debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.5.4"

  // Unit test dependencies
  testImplementation "junit:junit:4.12"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.2.41"
  testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
  testImplementation "org.assertj:assertj-core:3.8.0"
  testImplementation "org.robolectric:robolectric:3.8"
  testImplementation "androidx.arch.core:core-testing:2.0.0-alpha1"

  // Instrumentation test dependencies
  androidTestImplementation "junit:junit:4.12"
  androidTestImplementation "com.nhaarman:mockito-kotlin:1.5.0"
  androidTestImplementation "org.mockito:mockito-android:2.13.0"
  androidTestImplementation(
      "androidx.test.espresso:espresso-core:3.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation("androidx.test:runner:1.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation("androidx.test:rules:1.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation(
      "androidx.test.espresso:espresso-intents:3.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation(
      "androidx.test.espresso:espresso-contrib:3.1.0-alpha1") {
    exclude module: 'appcompat'
    exclude module: 'appcompat-v7'
    exclude module: 'support-v4'
    exclude module: 'support-v13'
    exclude module: 'support-annotations'
    exclude module: 'recyclerview-v7'
    exclude module: 'design'
  }

  kaptTest "com.google.dagger:dagger-compiler:2.16"
  kaptAndroidTest "com.google.dagger:dagger-compiler:2.16"
}

def getGitHash() {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}
