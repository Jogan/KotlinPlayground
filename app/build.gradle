ext {
  ci = System.getenv("CI") == "true"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
kapt {
  correctErrorTypes = true
}
apply plugin: 'kotlin-android-extensions'
androidExtensions {
  experimental = true
}

apply plugin: 'com.getkeepsafe.dexcount'
dexcount {
  // Only run the method counter on CI
  runOnEachPackage = ci
}

android {
  buildToolsVersion "28.0.2"
  compileSdkVersion 28

  defaultConfig {
    applicationId "com.jogan.kotlinplayground"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner 'com.jogan.kotlinplayground.test.TestRunner'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }

  dexOptions {
    // Don't pre-dex on CI
    preDexLibraries !ci
  }

  lintOptions {
    // Disable lintVital. Not needed since lint is run on CI
    checkReleaseBuilds false
  }

  // Always show the result of every unit test, even if it passes.
  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  def kotlin_version = "1.2.51"
  def jetpack_version = "1.0.0-rc01"
  def lifecycle_version = "2.0.0-rc01"
  def room_version = "2.0.0-rc01"
  def paging_version = "2.0.0-rc01"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  implementation "androidx.core:core-ktx:$jetpack_version"
  implementation "androidx.appcompat:appcompat:$jetpack_version"
  implementation "androidx.recyclerview:recyclerview:$jetpack_version"
  implementation "com.google.android.material:material:$jetpack_version"
  implementation "androidx.constraintlayout:constraintlayout:1.1.2"

  implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
  kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

  implementation "androidx.paging:paging-runtime:$paging_version"

  implementation "androidx.room:room-runtime:$room_version"
  implementation "androidx.room:room-rxjava2:$room_version"
  kapt "androidx.room:room-compiler:$room_version"

  // Other
  implementation "com.google.dagger:dagger-android:2.16"
  implementation "com.google.dagger:dagger-android-support:2.16"
  kapt "com.google.dagger:dagger-compiler:2.16"
  kapt "com.google.dagger:dagger-android-processor:2.16"

  implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"
  implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
  implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.1.1"

  implementation "javax.inject:javax.inject:1"

  kapt "com.squareup.moshi:moshi-kotlin-codegen:1.6.0"
  implementation "com.squareup.moshi:moshi:1.6.0"
  implementation "com.squareup.moshi:moshi-kotlin:1.6.0"
  implementation "com.squareup.okhttp3:okhttp:3.11.0"
  implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"
  implementation "com.squareup.retrofit2:retrofit:2.4.0"
  implementation "com.squareup.retrofit2:converter-moshi:2.4.0"
  implementation "com.squareup.retrofit2:adapter-rxjava2:2.4.0"

  implementation "com.github.bumptech.glide:glide:4.7.1"

  implementation "com.xwray:groupie:2.1.0"
  implementation "com.xwray:groupie-kotlin-android-extensions:2.1.0"

  implementation "com.jakewharton.timber:timber:4.7.1"

  debugImplementation "nl.littlerobots.rxlint:rxlint:1.6"

  debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.1"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.1"

  // Unit test dependencies
  testImplementation "junit:junit:4.12"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
  testImplementation "org.assertj:assertj-core:3.8.0"
  testImplementation "org.robolectric:robolectric:3.8"

  // Instrumentation test dependencies
  androidTestImplementation "junit:junit:4.12"
  androidTestImplementation "com.nhaarman:mockito-kotlin:1.6.0"
  androidTestImplementation "org.mockito:mockito-android:2.19.0"
  androidTestImplementation(
      "androidx.test.espresso:espresso-core:3.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation("androidx.test:runner:1.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation("androidx.test:rules:1.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation(
      "androidx.test.espresso:espresso-intents:3.1.0-alpha1") {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation("androidx.test.espresso:espresso-contrib:3.1.0-alpha1") {
    exclude module: 'appcompat'
    exclude module: 'appcompat-v7'
    exclude module: 'support-v4'
    exclude module: 'support-v13'
    exclude module: 'support-annotations'
    exclude module: 'recyclerview-v7'
    exclude module: 'design'
  }

  kaptTest "com.google.dagger:dagger-compiler:2.16"
  kaptAndroidTest "com.google.dagger:dagger-compiler:2.16"
}

def getGitHash() {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}
